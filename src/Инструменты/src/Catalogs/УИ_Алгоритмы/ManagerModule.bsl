
Функция КоллекцияВТЗ(Коллекция) Экспорт
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("Значение");
	Если Не ТипЗнч(Коллекция) = Тип("Массив") Тогда
		ТЗ.Колонки.Добавить("Ключ");
		Для Каждого ТекЭлем Из Коллекция Цикл
			НС=ТЗ.Добавить();
			ЗаполнитьЗначенияСвойств(НС, ТекЭлем);
		КонецЦикла;
	Иначе
		Для I = 0 По Коллекция.ВГраница() Цикл
			ТЗ.Добавить();
		КонецЦикла;
		ТЗ.ЗагрузитьКолонку(Коллекция, 0);
	КонецЕсли;
	Возврат ТЗ;
КонецФункции

Процедура ДобавитьКолонкуНС(Форма, Знач КолонкаИмя, ОписаниеТипаКолонки, ТаблицаФормы) Экспорт
	МассивДобавляемыхРеквизитов = Новый Массив;
	МассивДобавляемыхРеквизитов.Добавить(
	Новый РеквизитФормы(КолонкаИмя, ОписаниеТипаКолонки, ТаблицаФормы, КолонкаИмя));
	Форма.ИзменитьРеквизиты(МассивДобавляемыхРеквизитов);
	НовыйЭлемент = Форма.Элементы.Добавить(КолонкаИмя, Тип("ПолеФормы"), Форма.Элементы[ТаблицаФормы]);
	НовыйЭлемент.Заголовок=КолонкаИмя;
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = ТаблицаФормы + "." + КолонкаИмя;
КонецПроцедуры

Процедура УдалитьКолонкуНС(Форма, Знач КолонкаИмя, ТаблицаФормы) Экспорт
	Форма.Элементы.Удалить(Форма.Элементы.Найти(КолонкаИмя));
	МассивУдаляемыхРеквизитов = Новый Массив;
	МассивУдаляемыхРеквизитов.Добавить(ТаблицаФормы + "." + КолонкаИмя);
	Форма.ИзменитьРеквизиты( , МассивУдаляемыхРеквизитов);
КонецПроцедуры
