&Вместо("ПодключитьВнешнююОбработку")
Функция УИ_ПодключитьВнешнююОбработку(Ссылка) Экспорт
	УИ_НастройкаОтладки=УИ_ОбщегоНазначения.НастройкиОтладкиДополнительнойОбработки(Ссылка);

	УИ_ЕстьОтладка=Ложь;
	Если УИ_НастройкаОтладки.ОтладкаВключена И ЗначениеЗаполнено(УИ_НастройкаОтладки.ИмяФайлаНаСервере) Тогда
		Если УИ_НастройкаОтладки.Пользователь = Неопределено Или Не ЗначениеЗаполнено(УИ_НастройкаОтладки.Пользователь) Тогда

			УИ_ЕстьОтладка=Истина;
		ИначеЕсли УИ_НастройкаОтладки.Пользователь = Пользователи.ТекущийПользователь() Тогда
			УИ_ЕстьОтладка=Истина;

		КонецЕсли;
	КонецЕсли;

	Если Не УИ_ЕстьОтладка Тогда
		Возврат ПродолжитьВызов(Ссылка);
	Иначе

		УИ_ФайлОбработки = Новый Файл(УИ_НастройкаОтладки.ИмяФайлаНаСервере);
		Если Не УИ_ФайлОбработки.Существует() Тогда

			УИ_ХранилищеОбработки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ХранилищеОбработки");
			УИ_ДвоичныеДанные = УИ_ХранилищеОбработки.Получить();
			УИ_ДвоичныеДанные.Записать(УИ_НастройкаОтладки.ИмяФайлаНаСервере);

		КонецЕсли;

		УИ_Вид = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Вид");
		Если УИ_Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.Отчет
			 Или УИ_Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительныйОтчет Тогда
			УИ_Менеджер = ВнешниеОтчеты;
		Иначе
			УИ_Менеджер = ВнешниеОбработки;
		КонецЕсли;

		УИ_ОписаниеЗащитыОтОпасныхДействий = Новый ОписаниеЗащитыОтОпасныхДействий;
		УИ_ОписаниеЗащитыОтОпасныхДействий.ПредупреждатьОбОпасныхДействиях = Ложь;

		УИ_ОбработкаОбъект = УИ_Менеджер.Создать(УИ_НастройкаОтладки.ИмяФайлаНаСервере,
												 Ложь,
												 УИ_ОписаниеЗащитыОтОпасныхДействий);

		Возврат СокрЛП(УИ_ОбработкаОбъект.Метаданные().Имя);
	КонецЕсли;

КонецФункции
